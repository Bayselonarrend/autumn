#Использовать asserts

Перем _ПрименятьТолькоНапильники;
Перем _НеПрименятьНапильники;

Функция НапильникПрименяется(ИмяНапильника) Экспорт

	Если _ПрименятьТолькоНапильники.Количество() > 0 Тогда
		Возврат _ПрименятьТолькоНапильники.Найти(ИмяНапильника) <> Неопределено;
	Иначе
		Возврат _НеПрименятьНапильники.Найти(ИмяНапильника) = Неопределено;
	КонецЕсли;

КонецФункции

&Аннотация("ОсобоеОбращение")
Процедура ПриСозданииОбъекта(
	&Повторяемый ПрименятьТолькоНапильник = Неопределено,
	&Повторяемый НеПрименятьНапильник = Неопределено
)
	
	Для Каждого МестоПрименения Из ПрименятьТолькоНапильник Цикл
		Ожидаем.Что(МестоПрименения, "Ограничение ""только"" применения напильника должно быть задано строкой")
			.ИмеетТип("Строка");
	КонецЦикла;
	
	Для Каждого МестоПрименения Из НеПрименятьНапильник Цикл
		Ожидаем.Что(МестоПрименения, "Ограничение ""не"" применения напильника должно быть задано строкой")
			.ИмеетТип("Строка");
	КонецЦикла;
	
	Если ПрименятьТолькоНапильник.Количество() > 0 И НеПрименятьНапильник.Количество() > 0 Тогда
		ВызватьИсключение "Нельзя одновременно задавать ограничения ""только"" и ""не""";
	КонецЕсли;
	
	_ПрименятьТолькоНапильники = ПрименятьТолькоНапильник;
	_НеПрименятьНапильники = НеПрименятьНапильник;
	
КонецПроцедуры
